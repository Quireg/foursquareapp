apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.secrets");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext {
    supportLibraryVersion = '26.1.0'
    butterknifeVersion = "8.8.1"
    leakCanaryVersion = "1.5.4"
    dagger2Version = "2.11"
    moxyVersion = "1.5.3"
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "ua.in.quireg.foursquareapp"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "CLIENT_ID", keystoreProperties['CLIENT_ID'])
        buildConfigField("String", "CLIENT_SECRET", keystoreProperties['CLIENT_SECRET'])
        buildConfigField("long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Design
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //Leaks detection
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    //Support lib
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:preference-v7:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"

    //Views binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //Navigation
    implementation 'ru.terrakok.cicerone:cicerone:2.1.0'

    //Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'

    //Pictures
    implementation 'com.squareup.picasso:picasso:2.5.2'

    //DI
    implementation "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"

    //Reactive
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'

    //Http stack
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.3.0'
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.3.0'
    implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava2', version: '2.3.0'

    //MVP
    implementation "com.arello-mobile:moxy:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxyVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'


}
